VB6 Tutorial 54: Collections

<p>Collections are objects in Visual Basic that are used to store a group of data values. Collections are similar to array. But there are some special features that differentiate the collections from arrays. </p>

<p>We have seen that array elements must be of the same data type. But the Collection members can be of any data type and you don't need to set the size of the Collection object. You can easily add items to the collection and it will grow accordingly.</p>
<h3>Creating a Collection</h3>
<p>To use a Collection in your code, you first need to declare and create it.</p>
<h4>Example</h4>
<p><pre>Dim names As Collection     'Declaring the Collection
Set names = New Collection   'Creating the Collection</pre></p>
<p>Or, replace the above code with this one line code</p>
<p><pre>Dim names As New Collection   'Declaration and creation</pre></p>
<h3>The Add method</h3>
<p>You can add one item at a time using the Add method. </p>
<h4>Example</h4> 
<p><pre>Dim names As New Collection
names.Add "john"
names.Add "david"</pre></p>
<p>You may use a string key associated with the item. The string key is used to refer to a Collection item.</p>
<p><pre>Dim names As New Collection
names.Add "John", "one"   ' "one" is the string key, used to refer to the item</pre></p>
<h3>The Item method</h3>
<p>You can refer to a particular item of the collection using the Item method. Here you may use either the index or key value. The index value starts from 1.</p>
<h4>Syntax</h4>
<p><pre>Collection_Name.Item(Index)</pre></p>
<p>or<p>          
<p><pre>Collection_Name.Item(Key)</pre></p>
<h4>Example</h4> 
<p>Retrieving a particular item using the index of the item</p>
<p><pre>Private Sub cmdShow_Click()
    Dim names As New Collection

    names.Add "John", "one"
    names.Add "David"

    Print names.Item(2) '2 is the index of the item

End Sub</pre></p>
<h4>Output</h4> 
<p><pre>David</pre></p>
<h4>Example</h4> 
<p>Retrieving a particular item using the string key of the item</p>
<p><pre>Private Sub cmdShow_Click()
    Dim names As New Collection

    names.Add "John", "one"
    names.Add "David"

    Print names.Item("one")

End Sub</pre></p>
<h4>Output</h4> 
<p><pre>John</pre></p>
<p>The Item method is the default member of the Collection class, so you may omit it in your code.</p>
<h4>Example</h4>
<p><pre>Private Sub cmdShow_Click()
    Dim names As New Collection

    names.Add "John", "one"
    names.Add "David"

    Print names("one")
    Print names(2)

End Sub</pre></p>
<h4>Output</h4> 
<p><pre>John
David</pre></p>
<h3>Before and After</h3>
<p>You can choose to store the item values exactly where you want using the Before and After argument of the Add method.</p>
<h4>Example</h4>
<p><pre>Private Sub cmdShow_Click()

     Dim items As New Collection

     items.Add "one"
     items.Add "two"
     items.Add "three", , 1

     For i = 1 To 3
        Print items.Item(i)
     Next i
End Sub</pre></p>
<h4>Output</h4>
<p><pre>three
one
two</pre></p>
<h3>Remove</h3>
<p>You can remove a particular item from the Collection using the Remove method.</p>
<h4>Example</h4>
<p><pre>Private Sub Command1_Click()
     Dim country As New Collection
     country.Add "USA"
     country.Add "UK"
     country.Add "Japan", "j"

     country.Remove (1)
     country.Remove ("j")

End Sub</pre></p>
<h3>Count</h3>
<h4>Example</h4>
<p><pre>Private Sub cmdCount_Click()
     Dim country As New Collection
     country.Add "USA"
     country.Add "India"
     country.Add "Japan", "j"

     Dim n As Integer
     n = country.Count
     Print n

End Sub</pre></p>

<h4>Output</h4>
<p><pre>3</pre></p>
<h3>Retrieving the last item </h3>
<h4>Example</h4> 
<p><pre>Private Sub Command1_Click()
     Dim country As New Collection
     country.Add "Germany"
     country.Add "India"
     country.Add "China", "j"

     Print country.Item(country.Count)
End Sub</pre></p>
<h4>Output</h4> 
<p><pre>China</pre></p>
<h3>Delete all items</h3>
<p>Use a Do While loop to delete all item from the Collection object.</p>
<h4>Example</h4>  
<p><pre>Private Sub cmdDeleteAll_Click()
     Dim country As New Collection
     country.Add "Bangladesh"
     country.Add "Australia"
     country.Add "Russia", "j"

     Do While (country.Count > 0)
        country.Remove 1
     Loop

     Print country.Count
End Sub</pre></p>
<h4>Output</h4> 
<p><pre>0</pre></p>
<p>Another way to delete all items is to destroy the Collection object. The following code destroys the Collection object and thus deletes all the items.</p>
<p><pre>Set items = Nothing</pre></p>
<p>Or</p>
<p><pre>Set items = New Collection</pre></p>